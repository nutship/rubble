<font class="i_r_a%10&10_b%10&0" id="1-bit floating point">

<img src="../img/stackframes.png" width=330>

### 1. 过程

过程是重要的机器级抽象，要提供对它的机器级支持，需要依次执行以下机制 (以 P 调用 Q, Q 返回 P 为例):

-   保存寄存器的状态:&ensp; P 和 Q 各负其责
-   传递数据:&ensp; P 向 Q 传递多个参数，Q 向 P 返回一个值
-   转移控制:&ensp; P 进入 Q 和 Q 返回 P 时，正确设置 PC 的值
-   分配和释放内存:&ensp; Q 可能需要局部空间，并在返回 P 前释放

#### (1). 传递数据

P 调用 Q 时，先要把参数复制到适当的寄存器中:

-   x86-64 中，可以通过寄存器最多传递 6 个整型参数，超过 6 个的部分通过栈传递，且参数 7 位于栈顶方向、所有参数向 8 字节对齐
-   访问的寄存器名取决于数据大小，例如，如果参数 1 是 32 位的，可通过 `%edi` 访问

</font>

??? adcodes "example"

    === "&emsp;C&emsp;"

        ```C
        void proc(long  a1, long*  a1p, int  a2, int*  a2p,
                  short a3, short* a3p, char a4, char* a4p) {
            *a1p += a1;
            *a2p += a2;
            *a3p += a3;
            *a4p += a4;
        }
        ```

    === "&emsp;assembly&emsp;"

        ```asm
        ;Arguments passed as follows:
        ; a1   in %rdi (64),   a1p  in %rsi (64)
        ; a2   in %edx (32),   a2p  in %rcx (64)
        ; a3   in %r8w (16),   a3p  in %r9  (64)
        ; a4   at %rsp+8 (8),  a4p  at %rsp+16 (64)
        proc:
            movq	16(%rsp), %rax
            addq	%rdi, (%rsi)
            addl	%edx, (%rcx)
            addw	%r8w, (%r9)
            movl	8(%rsp), %edx
            addb	%dl, (%rax)
            ret
        ```

#### (2). 转移控制

假设紧跟 `call` 指令之后的地址是 `A`，`Label` 的起始地址是 `B`

| instruction     | effect                               |
| :-------------- | :----------------------------------- |
| `call Label`    | `pushq A`, &ensp;`mov B, %rip`       |
| `call *Operand` | `pushq A`, &ensp;`mov Operand, %rip` |
| `ret`           | `popq %rip`                          |

#### (3). 栈上的局部存储

为提高效率，x86-64 会尽可能少地使用栈帧，局部变量很少的叶子函数甚至不需要栈帧。有些局部数据必须存储在栈中:

-   寄存器不足时的本地数据
-   被 `&` 取地址的局部变量
-   数组或结构体

??? adcodes "example"

    === "original C code"

        ```C
        long swapAdd(long* xp, long* yp) {
            long x = *xp;
            long y = *yp;
            *xp = y;
            *yp = x;
            return x + y;
        }

        long caller() {
            long arg1 = 534;
            long arg2 = 1057;
            long sum = swapAdd(&arg1, &arg2);
            long diff = arg1 - arg2;
            return sum * diff;
        }
        ```

    === "assembly of caller()"

        ```asm
        caller():
            subq    $16, %rsp      ;allocate 16 bytes
            movq    $534, (%rsp)
            movq    $1057, 8(%rsp)
            leaq    8(%rsp), %rsi  ;&arg1
            movq    %rsp, %rdi     ;&arg2
            call    swapAdd
            movq    (%rsp), %rdx   ;get arg1
            subq    8(%rsp), %rdx
            imulq   %rdx, %rax
            addq    $16, %rsp      ;deallocate
            ret
        ```

运行时栈提供了一种简单的、在需要时分配、在完成时释放的机制。

#### (4). 保存寄存器的状态

寄存器是唯一被所有过程共享的资源，需要保证被调用者不会覆盖调用者稍后使用的寄存器值，对此，x86-64 的惯例是:

-   `%rbx, %rbp, %r8-%r15` 需要被调用者保存
-   除了 `%rsp` 及以上 的所有其他寄存器需要调用者保存

=== "original C code"

    ```C
    long Q(long);

    long P(long x, long y) {
        long u = Q(y);
        long v = Q(x);
        return u + v;
    }
    ```

=== "assembly"

    ```asm
    P:
        pushq	%rbp         ;save %rbp
        pushq	%rbx         ;save %rbx
        subq	$8, %rsp     ;align stack frame
        movq	%rdi, %rbp   ;save x
        movq	%rsi, %rdi   ;move y to first argument
        call	Q
        movq	%rax, %rbx   ;save result=Q(y)
        movq	%rbp, %rdi   ;move x to first argument
        call	Q
        addq	%rbx, %rax
        addq	$8, %rsp     ;deallocate last part of stack
        popq	%rbx         ;restore %rbx
        popq	%rbp         ;restore %rbp
        ret
    ```

从例子中可以看到:

-   P 作为 callee，需要保存及恢复后续被使用的 `%rbp, %rbx`
-   P 作为 caller，需要保存 `%rdi (x)` 和 `%rax (u)`

### 2. 对抗缓冲区溢出攻击

由于 C 不对数组越界做检查，可以通过字符串覆盖返回地址并插入攻击代码的字节编码，实现攻击。

#### (1). 栈随机化

其思想是随机填充栈，使得每次运行时栈的位置都有变化。在 Linux 中，这种机制已被泛用 (地址空间布局随机化, ASLR)，程序代码、库代码、栈、全局变量和堆数据每次运行时都会加载到内存的不同区域，这样可以对抗一些形式的攻击。

#### (2). 栈破坏检测

gcc 默认会加入 stack protector 机制: 在局部缓冲区和栈状态之间存储一个金丝雀值 (canary)，由程序每次运行随机产生，在恢复寄存器状态从函数返回之前，程序会检查这个值是否被修改。

```C
void echo() {
    char buf[8];
    gets(buf);
}
```

如果不加 `-fno-stack-protector` 选项，该函数会编译为:

```asm
echo:
	subq	$24, %rsp
	movq	%fs:40, %rax
	movq	%rax, 8(%rsp)
	xorl	%eax, %eax
	movq	%rsp, %rdi
	call	gets
	movq	8(%rsp), %rax
	subq	%fs:40, %rax
	jne	.L4
	addq	$24, %rsp
	ret
.L4:
	call	__stack_chk_fail
```

`%fs:40` 指明金丝雀值是用段寻址读入的，返回地址在 `%rsp+24`，金丝雀值在 `%rsp+8`。

#### (3). 限制可执行代码区域

限制内存只有某部分区域可以执行，其他部分只能读或写。
